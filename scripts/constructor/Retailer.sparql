# Construct quad with Retailler as the subject to 1 hop object (string integer Offer User)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
    GRAPH ?g {
         ?s_translated rdf:type <http://example.org/federated_shop/Retailer> .
         ?s_translated ?p_translated ?o_translated 
     }
} WHERE { 
 ?s ?p ?o .
 FILTER CONTAINS(STR(?s),"Retailer") .

 BIND(REPLACE(STR(?s), STR("http://example.org/_/Retailer_"), STR("s")) AS ?site_id) .
 BIND(CONCAT(?site_id, "/") AS ?site_id_translated)
 BIND(URI(CONCAT("http://example.org/", ?site_id_translated)) AS ?g) .
 BIND(URI(REPLACE(STR(?s), "_/", ?site_id_translated)) AS ?s_translated) .
 BIND(URI(REPLACE(STR(?p), "_/", "")) AS ?p_translated) .

 BIND(
        IF(
            REGEX(STR(?o),"http://example.org/_/string_"),
            STR(
                REPLACE(
                    STR(?o),"http://example.org/_/",""
                )
            ),
            IF(
                REGEX(STR(?o),"http://example.org/_/integer_"),
                xsd:integer(
                    REPLACE(
                        STR(?o),"http://example.org/_/integer_",""
                    )
                ),
                IF(
                    REGEX(STR(?o),"http://example.org/_/date_"),
                    xsd:integer(
                        REPLACE(
                            STR(?o),"http://example.org/_/date_",""
                        )
                    ),
                    URI(REPLACE(STR(?o), "_/", ?site_id_translated))
                )
            )
        ) AS ?o_translated
    )
}